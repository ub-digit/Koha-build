#!/usr/bin/perl

use strict;
use warnings;

use CGI qw ( -utf8 );
use XML::Simple;
use C4::Auth qw/check_api_auth/;
use C4::Letters qw( GetPreparedLetter EnqueueLetter );
use C4::Reserves;

my $query = new CGI;
binmode STDOUT, ':encoding(UTF-8)';

my ($status, $cookie, $sessionID) = check_api_auth($query, { editcatalogue => 'edit_catalogue'} );
unless ($status eq "ok") {
  print $query->header(-type => 'text/xml', -status => '403 Forbidden');
  print XMLout({ status => $status }, NoAttr => 1, RootName => 'response', XMLDecl => 1);
  exit 0;
}

my $reserve_id = $query->param('reserve_id');
my $cardnumber = $query->param('cardnumber');
my $reserve = Koha::Holds->find($reserve_id);
my $patron = Koha::Patrons->find({cardnumber => $cardnumber});

if (!$reserve || !$patron || $reserve->borrowernumber() != $patron->borrowernumber()) {
  print $query->header(-type => 'text/xml', -status => '404');
  print XMLout({ status => "Reserve not found", error_code => "reserveNotFound"  }, NoAttr => 1, RootName => 'response', XMLDecl => 1);
  exit 0;
}

if($reserve_id) {
  $reserve->cancel;
}

if($reserve->is_waiting()) {
  # Create a Letter from HOLD_WAITING_CANCEL template
  # and send to email address of the library where the reservation is waiting.
  my $pickup_library = $reserve->branch();
  my $pickup_library_email = $pickup_library->branchemail();
  my $letter = C4::Letters::GetPreparedLetter(
    module                 => 'reserves',
    letter_code            => 'HOLD_WAITING_CANCEL',
    message_transport_type => 'email',
    branchcode             => $reserve->borrower->branchcode,
    lang                   => $reserve->borrower->lang,
    tables => {
        branches    => $reserve->borrower->branchcode,
        borrowers   => $reserve->borrowernumber,
        items       => $reserve->itemnumber,
        biblio      => $reserve->biblionumber,
        biblioitems => $reserve->biblionumber,
        reserves    => $reserve->unblessed,
    }
  );

  if ($letter) {
      C4::Letters::EnqueueLetter(
          {
              letter => $letter,
              to_address => $pickup_library_email,
              from_address => $pickup_library_email,
              message_transport_type => 'email',
          }
      );
  }
}

print $query->header(-type => 'text/xml',-charset => 'utf-8', -status => '200');
print XMLout({ status => "ok" }, NoAttr => 1, RootName => 'response', XMLDecl => 1);
exit 0;
